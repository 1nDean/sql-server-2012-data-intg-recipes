-- Apply an XSD
DECLARE @XSD XML;
SELECT @XSD = CONVERT(XML, XSDDef) FROM OPENROWSET (BULK 'C:\SQL2012DIRecipes\CH03\Clients_Simple.xsd', SINGLE_BLOB) AS XSD (XSDDef)
CREATE XML SCHEMA COLLECTION XML_XSD AS @XSD;
GO
-- Staging table to hold XML data
CREATE TABLE dbo.Tmp_XMLLoad(
ID int IDENTITY(1,1) NOT NULL,
XMLData xml(CONTENT dbo.XML_XSD) NULL,
CONSTRAINT PK_Tmp_XMLLoad PRIMARY KEY CLUSTERED
(
ID ASC
)WITH (PAD_INDEX = OFF, STATISTICS_NORECOMPUTE = OFF, IGNORE_DUP_KEY = OFF, ALLOW_ROW_LOCKS = ON, ALLOW_PAGE_LOCKS = ON)
);
GO
-- XML index
CREATE PRIMARY XML INDEX XX_XMLData ON Tmp_XMLLoad (XMLData)
CREATE XML INDEX SX_XMLData_Property ON Tmp_XMLLoad (XMLData) USING XML INDEX XX_XMLData FOR PROPERTY
CREATE XML INDEX SX_XMLData_Value ON Tmp_XMLLoad (XMLData) USING XML INDEX XX_XMLData FOR VALUE
CREATE XML INDEX SX_XMLData_Path ON Tmp_XMLLoad (XMLData) USING XML INDEX XX_XMLData FOR PATH

-- Load data into table
INSERT INTO Tmp_XMLLoad (XMLData)
SELECT CAST(XMLSource AS XML) AS XMLSource
FROM OPENROWSET(BULK 'C:\SQL2012DIRecipes\CH03\Clients_Simple.xml', SINGLE_BLOB) AS X (XMLSource)
-- and to output:
INSERT INTO XmlImport_Clients (ID, ClientName, Town, Country)
SELECT
SRC.Client.value('ID[1]', 'INT') AS ID
,SRC.Client.value('ClientName[1]', 'VARCHAR(50)') AS ClientName
,SRC.Client.value('Town[1]', 'VARCHAR(50)') AS Town
,SRC.Client.value('Country[1]', 'INT') AS Country
FROM Tmp_XMLLoad
CROSS APPLY XMLData.nodes('CarSales/Client') AS SRC (Client) ;